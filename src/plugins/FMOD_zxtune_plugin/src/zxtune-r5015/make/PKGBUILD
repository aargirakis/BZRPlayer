# Maintainer: Ian D. Scott <ian@perebruin.com>
pkgname=zxtune-git
pkgver=VERSION
pkgrel=1
pkgdesc="Portable toolkit for ZX-Spectrum music playing"
arch=('i686' 'x86_64')
url="https://zxtune.bitbucket.io"
license=('GPL3')
depends=('boost-libs' 'gcc-libs' 'qt4' 'zlib')
optdepends=(
'alsa-lib: for ALSA output support'
'lame: for conversion to .mp3 format'
'libvorbis: for conversion to .ogg format'
'flac: for conversion to .flac format'
'curl: for accessing data via network schemes'
)
makedepends=('git' 'gcc' 'zip' 'boost')
provides=('zxtune')
options=(!strip !docs !libtool !emptydirs !zipman makeflags)
source=('git+https://bitbucket.org/zxtune/zxtune.git')
md5sums=('SKIP')
_qt4_options="qt.includes=/usr/include/qt4 tools.uic=uic-qt4 tools.moc=moc-qt4 tools.rcc=rcc-qt4"

pkgver() {
  cd zxtune
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/zxtune"
  make platform=linux system.zlib=1 ${_qt4_options} -C apps/bundle
}

package() {
  cd "${srcdir}/zxtune"
  make DESTDIR="${pkgdir}" platform=linux install_linux -C apps/bundle
}
