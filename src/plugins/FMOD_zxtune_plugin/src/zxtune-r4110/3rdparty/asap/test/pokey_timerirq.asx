	ift	SAP
	opt	h-
	dta	c'SAP',13,10
	dta	c'AUTHOR "Piotr Fusik (0xF)"',13,10
	dta	c'NAME "POKEY Timer IRQ Test"',13,10
	dta	c'DATE "07/06/2011"',13,10
	dta	c'TYPE D',13,10
	dta	c'INIT 3000',13,10
	dta	c'TIME 00:10',13,10
	opt	h+
	eif

scrptr	equ	$8a	; 2 bytes
printptr	equ	$8c	; 2 bytes

	org	f:$3000
main
:SAP	mwa	#$1000	scrptr
	lda	<title
	ldx	>title
	jsr	print

:!SAP	sei:inc	^4e
:!SAP	inc	^40
:!SAP	mva	#$fe	^31

	ldx	#8
test_loop
	mwa	#irq	$fffe
	mva	#0	^2f
	sta	^2e
	cli
	mva	#$40	^28
	mva	#$ff	^20
	sta	^29
	stx	^20
	sta	^4a
	mva	#3	^2f
	lsr	@
	sta	^2e
	sta	^4a
	sta	^29
	jmp	*

irq
	lda	^2a
	cmp	expected,x
	bne	error
	dex
	bpl	test_loop

:!SAP	mva	$10	^2e
:!SAP	mva	#$ff	^31
:!SAP	lsr:cli	^4e
	lda	<pass
	ldx	>pass
	jsr	print
	mwa	#$af30	$d200
	jmp	*

error
	pha
	txa:pha
	mva	#0	^28
:!SAP	mva	$10	^2e
:!SAP	mva	#$ff	^31
:!SAP	lsr:cli	^4e
	lda	<fail
	ldx	>fail
	jsr	print
	pla
	jsr	put_hex_byte
	pla
	jsr	put_hex_byte
	lda	#$9b
	jsr	put_char
	mwa	#$cfa0	$d200
	jmp	*

put_hex_byte
	pha
:4	lsr	@
	jsr	put_hex_nibble
	pla
	and	#$f
put_hex_nibble
	cmp	#10
	sed
	adc	#'0'
	cld
put_char
	ift	SAP
	sta	(scrptr),0
	inw	scrptr
	rts
	els
	mvx	#11	$342
	mwx	#0	$348
	jmp	$e456
	eif

print
	sta	printptr
	stx	printptr+1
	jmp	print_2
print_1
	jsr	put_char
	inw	printptr
print_2
	lda	(printptr),0
	bne	print_1
	rts

expected
:3	dta	$30
:3	dta	$86
:3	dta	$10

title	dta	c'POKEY: Timer IRQ...',0
pass	dta	c'Pass',$9b,0
fail	dta	c'FAIL.',$9b,0

	ift	!SAP
	run	main
	eif
