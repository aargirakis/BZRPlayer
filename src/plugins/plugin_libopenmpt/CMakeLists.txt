cmake_minimum_required(VERSION 3.28)

set(NAME "libopenmpt")
set(VERSION "0.8.3")
project(plugin_${NAME})
set(PLUGIN_${NAME}_LIB "${PROJECT_NAME}.${LIB_EXT}" PARENT_SCOPE)
set(PLUGIN_${NAME}_NAME "${NAME}" PARENT_SCOPE)
set(PLUGIN_${NAME}_VERSION "${VERSION}" PARENT_SCOPE)

find_package(ZLIB REQUIRED)

set(CMAKE_C_STANDARD 17)
set(CMAKE_CXX_STANDARD 20)

set(NAME_VERSIONED "${NAME}-${VERSION}")
set(NAME_VERSIONED2 "${NAME}-${VERSION}+release")
set(FILENAME "${NAME_VERSIONED2}.makefile.tar.gz")
set(URL "https://lib.openmpt.org/files/${NAME}/src/${FILENAME}")
set(SHA_256_HASH "ba0dc8c058dcfc8ece5fcf21a4b8aa24371670f2263a075b69ad02fd2021a2b1")
set(UNPACKED_DIR "${NAME_VERSIONED2}")
set(MAKE_ARGS
        OPENMPT123=0 TEST=0 EXAMPLES=0 SHARED_LIB=0 STATIC_LIB=1
        NO_FLAC=1 NO_MPG123=1 NO_OGG=1 NO_PORTAUDIO=1 NO_PORTAUDIOCPP=1
        NO_PULSEAUDIO=1 NO_SDL2=1 NO_SNDFILE=1 NO_VORBIS=1 NO_VORBISFILE=1
)

if (WIN32)
    list(APPEND MAKE_ARGS MSYSTEM=UCRT64)
endif ()

if (CMAKE_BUILD_TYPE STREQUAL "Debug")
    list(APPEND MAKE_ARGS DEBUG=1)
endif ()

set(BUILD_BYPRODUCTS "bin/libopenmpt.a")
download_patch_and_make(
        ${NAME_VERSIONED} ${FILENAME} ${URL} ${SHA_256_HASH} true ${UNPACKED_DIR}
        ${CMAKE_CURRENT_SOURCE_DIR}/patches "" "${MAKE_ARGS}" false "${BUILD_BYPRODUCTS}"
)

file(GLOB SOURCES
        src/main.cpp

        ${CMAKE_SOURCE_DIR}/src/app/BaseRow.cpp)

add_library(${PROJECT_NAME} SHARED ${SOURCES})

add_dependencies(${PROJECT_NAME} ${NAME_VERSIONED})

target_include_directories(${PROJECT_NAME} PRIVATE
        ${EXTERNAL_SOURCE_DIR}/libopenmpt

        ${CMAKE_SOURCE_DIR}/src/app
        ${FMOD_API_DIR})

target_compile_definitions(${PROJECT_NAME} PRIVATE PLUGINS_CONFIG_DIR="${PLUGINS_CONFIG_DIR}")

add_library(LIBOPENMPT_STATIC STATIC IMPORTED GLOBAL)

set_target_properties(LIBOPENMPT_STATIC PROPERTIES
        IMPORTED_LOCATION "${EXTERNAL_SOURCE_DIR}/${BUILD_BYPRODUCTS}"
)

target_link_libraries(${PROJECT_NAME} PRIVATE
        LIBOPENMPT_STATIC
        ZLIB::ZLIB
)

set_target_properties(${PROJECT_NAME} PROPERTIES
        OUTPUT_NAME "${PROJECT_NAME}"
        PREFIX ""
        LIBRARY_OUTPUT_DIRECTORY ${OUTPUT_DIR}/${LIB_DIR}/${PLUGINS_DIR}
        RUNTIME_OUTPUT_DIRECTORY ${OUTPUT_DIR}/${LIB_DIR}/${PLUGINS_DIR}
)
